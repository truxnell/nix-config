{
  config,
  lib,
  pkgs,
  nixpkgs,
  inputs,
  imports,
  modulesPath,
  ...
}:

with lib;

let
  ifTheyExist = groups: builtins.filter (group: builtins.hasAttr group config.users.groups) groups;
in
{
  # NOTE
  # Some 'global' areas have defaults set in their respective modules.
  # These will be applied when the modules are loaded
  # Not the global role.
  # Not sure at this point a good way to manage globals in one place
  # without mono-repo config.

  imports = [
    (modulesPath + "/installer/scan/not-detected.nix") # Generated by nixos-config-generate
  ];

  config = {
    boot.tmp.cleanOnBoot = true;

    mySystem = {
      # basics for all devices
      time.timeZone = "Australia/Melbourne";
      security.increaseWheelLoginLimits = true;
      system.packages = with pkgs; [
        bat
        ntfs3g
        just
      ];
      domain = "trux.dev";
      internalDomain = "l.voltaicforge.com";

      shell.fish.enable = true;
      # But wont enable plugins globally, leave them for workstations
      system.resticBackup.remote.location = "s3:s3.us-west-002.backblazeb2.com/trux-backup-02f07ef6fe/nixos-restic";
    };

    environment.systemPackages = with pkgs; [
      curl
      wget
      dnsutils
      tree
      powertop
      usbutils
      parted
      smartmontools
      comma
      # Server role packages
      tmux
      btop
      # Dev role packages
      jq
      yq
      vim
      git
      nix
      dnscontrol # for updating internal DNS servers with homelab services
      nil
      nixpkgs-fmt
      statix
      nvd
      gh
      bind # for dns utils like named-checkconf
      inputs.nix-inspect.packages.${pkgs.system}.default
    ];

    networking.useDHCP = lib.mkDefault true;
    networking.domain = config.mySystem.domain;

    # This being enabled stops boot on any mount failing
    # which can be a pain with the NAS
    # as it makes it harder to troubleshoot-fix
    # than just continuing as far as possible
    systemd.enableEmergencyMode = false;

    # powerManagement.powertop.enable = true;

    ## Below is to align shell/system to flake's nixpkgs
    ## ref: https://nixos-and-flakes.thiscute.world/best-practices/nix-path-and-flake-registry

    # Make `nix repl '<nixpkgs>'` use the same nixpkgs as the one used by this flake.
    environment.etc."nix/inputs/nixpkgs".source = "${nixpkgs}";
    nix = {

      # make `nix run nixpkgs#nixpkgs` use the same nixpkgs as the one used by this flake.
      registry.nixpkgs.flake = nixpkgs;
      channel.enable = false; # remove nix-channel related tools & configs, we use flakes instead.

      # but NIX_PATH is still used by many useful tools, so we set it to the same value as the one used by this flake.
      # https://github.com/NixOS/nix/issues/9574
      settings.nix-path = lib.mkForce "nixpkgs=/etc/nix/inputs/nixpkgs";

      ###

      settings = {

        # Enable flakes
        experimental-features = [
          "nix-command"
          "flakes"
        ];

        # Substitutions
        substituters = [
          "https://nix-community.cachix.org"
          "https://numtide.cachix.org"
          "https://truxnell.cachix.org"
        ];

        trusted-public-keys = [
          "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
          "numtide.cachix.org-1:2ps1kLBUWjxIneOy1Ik6cQjb41X0iXVXeHigGmycPPE="
          "truxnell.cachix.org-1:ZhbK/e/SERuvwJVF8rX3qKrvk3JJylG0jLkeBBvbR68="
        ];

        # Fallback quickly if substituters are not available.
        connect-timeout = 25;
        # Avoid copying unnecessary stuff over SSH
        builders-use-substitutes = true;

        trusted-users = [
          "root"
          "@wheel"
        ];

        warn-dirty = false;

        # The default at 10 is rarely enough.
        log-lines = lib.mkDefault 25;

      };
    };

    system = {
      # Enable printing changes on nix build etc with nvd
      activationScripts.report-changes = ''
        PATH=$PATH:${
          lib.makeBinPath [
            pkgs.nvd
            pkgs.nix
          ]
        }
        nvd diff $(ls -dv /nix/var/nix/profiles/system-*-link | tail -2)
      '';

      # Do not change unless you know what you are doing
      stateVersion = "23.11"; # THERE BE DRAGONS

      #      (This one comes in the niiiiight)           :::
      #                                              :: :::.
      #                        \/,                    .:::::
      #            \),          \`-._                 :::888
      #            /\            \   `-.             ::88888
      #           /  \            | .(                ::88
      #          /,.  \           ; ( `              .:8888
      #             ), \         / ;``               :::888
      #            /_   \     __/_(_                  :88
      #              `. ,`..-'      `-._    \  /      :8
      #                )__ `.           `._ .\/.
      #               /   `. `             `-._______m         _,
      #   ,-=====-.-;'                 ,  ___________/ _,-_,'"`/__,-.
      #  C   =--   ;                   `.`._    V V V       -=-'"#==-._
      # :,  \     ,|      UuUu _,......__   `-.__A_A_ -. ._ ,--._ ",`` `-
      # ||  |`---' :    uUuUu,'          `'--...____/   `" `".   `
      # |`  :       \   UuUu:
      # :  /         \   UuUu`-._
      #  \(_          `._  uUuUu `-.
      #  (_3             `._  uUu   `._
      #                     ``-._      `.
      #                          `-._    `.
      #                              `.    \
      #                                )   ;
      #                               /   /
      #                `.        |\ ,'   /
      #                  ",_A_/\-| `   ,'
      #                    `--..,_|_,-'\
      #                           |     \
      #                           |      \__
      #                           |__

    };

    sops.age.sshKeyPaths = [
      (
        if config.mySystem.system.impermanence.enable then
          "/persist/etc/ssh/ssh_host_ed25519_key"
        else
          "/etc/ssh/ssh_host_ed25519_key"
      )
    ];

    sops.secrets.truxnell-password = {
      sopsFile = ./global-secrets.sops.yaml;
      neededForUsers = true;
    };

    users.users.truxnell = {
      isNormalUser = true;
      shell = pkgs.fish;
      hashedPasswordFile = config.sops.secrets.truxnell-password.path;
      extraGroups = [
        "wheel"
      ]
      ++ ifTheyExist [
        "network"
        "samba-users"
        "docker"
        "podman"
        "audio" # pulseaudio
        "libvirtd"
      ];

      openssh.authorizedKeys.keys = [
        "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIMZS9J1ydflZ4iJdJgO8+vnN8nNSlEwyn9tbWU9OcysW truxnell@home"
      ]; # TODO do i move to ingest github creds?

      # packages = [ pkgs.home-manager ];
    };

    # Role: Server configuration
    # Applied to all headless servers (raspi's, sbc, NUC etc)
    # Enable monitoring for remote scraping
    mySystem.services.monitoring.enable = true;
    mySystem.services.rebootRequiredCheck.enable = true;
    mySystem.security.wheelNeedsSudoPassword = false;
    mySystem.deploy.enable = true;
    mySystem.system.motd.enable = true;
    mySystem.services.gatus.monitors = [
      {
        name = config.networking.hostName;
        group = "servers";
        url = "icmp://${config.networking.hostName}.${config.mySystem.internalDomain}";
        interval = "1m";
        conditions = [ "[CONNECTED] == true" ];
      }
    ];

    nix.settings = {
      # TODO factor out into mySystem
      # Avoid disk full issues
      max-free = lib.mkDefault (1000 * 1000 * 1000);
      min-free = lib.mkDefault (128 * 1000 * 1000);
    };

    services.logrotate.enable = mkDefault true;

    services.smartd = mkDefault {
      enable = true;
      autodetect = true;
      defaults.autodetected = "-a -o on -S on -s (S/../.././02|L/../../6/03) -m root -M exec /run/smartdnotify";
    };

    systemd.services.smartdnotify = {
      description = "Forward smartd alerts to ntfy";
      serviceConfig = {
        Type = "oneshot";
        User = "root";
      };
      script = ''
        curl -H "tags:warning" -H "prio:high" \
            -d "$SMARTD_MESSAGE" \
            https://ntfy.trux.dev/smartd
      '';
    };

    # environment.noXlibs = mkDefault true;
    documentation = {
      enable = mkDefault false;
      doc.enable = mkDefault false;
      info.enable = mkDefault false;
      man.enable = mkDefault false;
      nixos.enable = mkDefault false;
    };
    programs.command-not-found.enable = mkDefault false;

    services.pulseaudio.enable = false;

    services.udisks2.enable = mkDefault false;
    # xdg = {
    #   autostart.enable = mkDefault false;
    #   icons.enable = mkDefault false;
    #   mime.enable = mkDefault true;
    #   sounds.enable = mkDefault false;
    # };

    programs.direnv = {
      # TODO move to home-manager
      enable = true;
      nix-direnv.enable = true;
    };

  };

}
